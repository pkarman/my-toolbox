#!/usr/bin/env perl
use strict;
use warnings;
use JSON;
use Search::Elasticsearch;
use File::Slurper qw( read_text read_binary );
use Try::Tiny;

sub read_json {
    my ($filename) = @_;
    from_json( read_binary($filename) );
}

my $e = Search::Elasticsearch->new();

if ( $ENV{CREATE} ) {
    $e->indices->delete( index => 'solar' );
    $e->indices->create( index => 'solar' );
    $e->indices->put_mapping(
        index => 'solar',
        type  => 'device',
        include_type_name => \1,
        body  => {
            properties        => {
                p_3phsum_kw     => { type => 'float' },    # AC power kWh
                p_mpptsum_kw    => { type => 'float' },    # AC power kWh
                ltea_3phsum_kwh => { type => 'float' },    # total energy kWh
                vln_3phavg_v    => { type => 'float' },    # AC voltage
                t_htsnk_degc    => { type => 'float' },    # heatsink temp
            }
        }
    );
    exit;
}

my @files = @ARGV ? @ARGV : <STDIN>;

for my $json_file (@files) {
    next unless -s $json_file;

    my $buf;
    try { $buf = read_json($json_file) };

    next unless $buf;

    my ($id) = ( $json_file =~ m/([\d\-]+)\.json/ );

    #warn $id;

    # each device is a "document"
    my $bulk = $e->bulk_helper( index => 'solar', type => 'device' );
    for my $device ( @{ $buf->{devices} } ) {
        my $id = join( "-", $id, $device->{SERIAL} );
        my @dtime = split( ',', $device->{CURTIME} );
        $device->{tstamp} = sprintf( "%s-%s-%sT%s:%s:%s", @dtime );
        $bulk->index( { id => $id, source => $device } );
    }
    $bulk->flush;
}

